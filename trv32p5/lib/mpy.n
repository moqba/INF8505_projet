/*
-- File : mpy.n
--
-- Contents : nML model for the trv32p5 processor -- MPY instructions.
--
-- Copyright (c) 2019-2021 Synopsys, Inc. This Synopsys processor model
-- captures an ASIP Designer Design Technique. The model and all associated
-- documentation are proprietary to Synopsys, Inc. and may only be used
-- pursuant to the terms and conditions of a written license agreement with
-- Synopsys, Inc.  All other use, reproduction, modification, or distribution
-- of the Synopsys processor model or the associated  documentation is
-- strictly prohibited.
*/

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~ Units & Transitories
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

fu mpy;
trn mpyA   <w32>;
trn mpyB   <w32>;
trn mpyR   <w32>;

trn mpyL   <w32>;       // low product
trn mpyH   <w32>;       // high product
trn mpyMD  <t2u>;       // mode: 0bAB, where A/B control signedness of mpyA/B
                        // 0b11: mpyA and mpyB are signed
                        // 0b10: mpyA is signed, mpyB is unsigned
                        // 0b00: mpyA and mpyB are unsigned

// avoid warning that one output is unused
property dead_end : mpyL, mpyH;

def md_mul    = 0b11;
def md_mulh   = 0b11;
def md_mulhsu = 0b10;
def md_mulhu  = 0b00;


// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~ Instruction nML Rules
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~ Top-Level Rule for this nML file

opn mpy_instrs (
    mpy_rrr_instr
);

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~ Multiplier
//
// mul    rd, rs1, rs2
// mulh   rd, rs1, rs2
// mulhsu rd, rs1, rs2
// mulhu  rd, rs1, rs2

opn mpy_rrr_instr (op: majOP_fn10, rd: mX1w_ME, rs1: mX1r_EX, rs2: mX2r_EX) {
  action {
    stage ID..EX:       mpyA`EX` = rs1;
                        mpyB`EX` = rs2;
    // ---
    stage EX..ME:
      switch (op) {
        case mul    :   mul_hw (mpyA`EX`,mpyB`EX`,mpyMD`EX`=md_mul`EX`,   mpyL`ME`,mpyH`ME`) @ mpy;
        case mulh   :   mul_hw (mpyA`EX`,mpyB`EX`,mpyMD`EX`=md_mulh`EX`,  mpyL`ME`,mpyH`ME`) @ mpy;
        case mulhsu :   mul_hw (mpyA`EX`,mpyB`EX`,mpyMD`EX`=md_mulhsu`EX`,mpyL`ME`,mpyH`ME`) @ mpy;
        case mulhu  :   mul_hw (mpyA`EX`,mpyB`EX`,mpyMD`EX`=md_mulhu`EX`, mpyL`ME`,mpyH`ME`) @ mpy;

        // low product
        case mul :
                        mpyR`ME` = mpyL`ME`;
        // high product
        case mulh|mulhsu|mulhu:
                        mpyR`ME` = mpyH`ME`;
      }
    // ---
    stage ME..WB:       rd = mpyR`ME`;
  }
  syntax : op PADMNM " " rd "," PADOP1 rs1 "," PADOP2 rs2;
  image  : op[9..3] :: rs2 :: rs1 :: op[2..0] :: rd :: opc32.OP
           class(mpy);
}


// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~ Chess Views for RTL Optimized Multiplication Primitive
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~ Multiplier

chess_view () {
   mpyR = mpyL; mul_hw (mpyA, mpyB, mpyMD=md_mul, mpyL, mpyH);
} -> {
   mpyR = mul    (mpyA, mpyB);
}

chess_view () {
   mpyR = mpyH; mul_hw (mpyA, mpyB, mpyMD=md_mulh, mpyL, mpyH);
} -> {
   mpyR = mulh   (mpyA, mpyB);
}

chess_view () {
   mpyR = mpyH; mul_hw (mpyA, mpyB, mpyMD=md_mulhsu, mpyL, mpyH);
} -> {
   mpyR = mulhsu (mpyA, mpyB);
}

chess_view () {
   mpyR = mpyH; mul_hw (mpyA, mpyB, mpyMD=md_mulhu, mpyL, mpyH);
} -> {
   mpyR = mulhu  (mpyA, mpyB);
}

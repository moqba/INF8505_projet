#!/bin/csh -f

# -- File : domake
# --
# -- Copyright (c) 2014 Synopsys Inc.

if ($#argv < 1) then
    echo "usage: domake <make target> [directories]"
    exit(1)
endif

set target = "$1"
shift argv

echo $target | grep -c "CFG=" > /dev/null
if ($? == 0) then
  set cfg=`echo $target | sed -e 's/.*CFG=\(\w*\)\([^\w].*\)\?/\1/'`
else
  set cfg="Release"
endif

if ($#argv == 0) then
    set directories = `glob "C[0-9][0-9]_*"`
else
    set directories = "$argv"
endif

if ( "$target" =~ virgin ) then
    # virgin cannot use make.all as it removes the .message file
    echo "$directories make $target"
    foreach x ($directories)
        cd $x
        make -f ../Makefile.test virgin
        cd ..
    end
else
   if ($?NCORES_HOST == 0) set NCORES_HOST = 1
   make -f make.all -j $NCORES_HOST MODE="$target" $directories
endif

switch ("$target")
    case diff*:
        set file = "test.diff"
        set prof = "test.prf"
      breaksw
    case rtldiff*:
        set file = "test.rtldiff"
      breaksw
endsw

if ( $?prof ) then
  set summary = "summary.$cfg.rpt"
  echo "summary of test results" > "$summary"
  echo "-------------------------------------" >> "$summary"
  foreach x ($directories)
      echo $x >> "$summary"
      grep "Total cycle count" $x/$cfg/$prof >> "$summary"
      grep "Total size in program memory" $x/$cfg/$prof >> "$summary"
  end
  echo "-----------------------" >> "$summary"
endif

# report failed tests when using a "diff" target
echo $target | grep -c "diff" > /dev/null
if ($? == 0) then
  echo
  echo "overview failing test results:"
  set errors = 0
  foreach x ($directories)
    grep "Total number of differences: 0" $x/$file > /dev/null
    if ($? != 0) then
      echo "ERROR in : $x"
      set errors = 1;
    endif
  end

  if ($errors == 0 ) then
    echo "all tests OK"
  else
    exit (1)
  endif
endif


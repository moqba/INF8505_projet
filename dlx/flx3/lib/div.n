/*
-- File : div.n
--
-- Contents : nML model for the FLX processor -- Divide instructions.
--
-- Copyright (c) 2014-2021 Synopsys, Inc. This Synopsys processor model
-- captures an ASIP Designer Design Technique. The model and all associated
-- documentation are proprietary to Synopsys, Inc. and may only be used
-- pursuant to the terms and conditions of a written license agreement with
-- Synopsys, Inc.  All other use, reproduction, modification, or distribution
-- of the Synopsys processor model or the associated  documentation is
-- strictly prohibited.
*/



opn div_instr( div_rr | fdiv_rr | fsqrt_r );

trn divA<w32>;
trn divB<w32>;

fu div;

//Control transitories used for hw_stall hazard rules
trn div_busy<uint1>;        hw_init div_busy     = 0; // Used to indicate division is active
trn div_busy_raw<uint1>;    hw_init div_busy_raw = 0; // Used for RAW hazard rule
trn div_busy_wb<uint1>;     hw_init div_busy_wb  = 0; // Used for WAW hazard rule
trn fdiv_busy<uint1>;       hw_init div_busy = 0;
trn fdiv_busy_raw<uint1>;   hw_init div_busy_raw = 0;

opn div_rr(s1: rexR1, s2: rexR2)
{
    action {
    stage ID..EX:
        divA=s1`EX`;
        divB=s2`EX`;
    stage EX:
        divide(divA, divB, PL = wl, PH = wh) @div;
    }
    syntax : "div " s1 "," s2;
    image  : opcode.function::s1::s2::"xxxxx"::function_code.div;
}

opn fdiv_rr(s1: rexR1, s2: rexR2)
{
    action {
    stage ID..EX:
        divA=s1`EX`;
        divB=s2`EX`;
    stage EX:
        fdivide(divA, divB, PL = wl) @div;
    }
    syntax : "fdiv " s1 "," s2;
    image  : opcode.function::s1::s2::"xxxxx"::function_code.fdiv;
}

opn fsqrt_r(s1: rexR1)
{
    action {
    stage ID..EX:
        divA=s1`EX`;
    stage EX:
        fsqrt(divA, PL = wl) @div;
    }
    syntax : "fsqrt " s1;
    image  : opcode.function::s1::"xxxxx"::"xxxxx"::function_code.fsqrt;
}


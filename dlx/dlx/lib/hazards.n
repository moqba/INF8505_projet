/*
-- File : hazards.n
--
-- Contents : nML model for the DLX processor - bypass and stall rules.
--
-- Copyright (c) 2014-2021 Synopsys, Inc. This Synopsys processor model
-- captures an ASIP Designer Design Technique. The model and all associated
-- documentation are proprietary to Synopsys, Inc. and may only be used
-- pursuant to the terms and conditions of a written license agreement with
-- Synopsys, Inc.  All other use, reproduction, modification, or distribution
-- of the Synopsys processor model or the associated  documentation is
-- strictly prohibited.
*/

// Hazards related to end-of-loop checking for zero-overhead loops:

//  A minimum offset is required from a jump instruction to the end-of-loop,
//  such that the jump instruction or any next instruction word still fetched
//  cannot trigger an end-of-loop check before the jump condition is known.
//  The following hazard rules are needed given the dlx PCU, where the
//  end-of-loop check happens based on the address of the instruction in the IF
//  stage, and is disabled (lp_update==0) when a jump signal is set or when no
//  PM fetch is done (issue_sig()==0 ).

//  a jump may not be the end-of-loop instruction, as the end-of-loop check (at
//  IF stage) would then come earlier than the jump action (ID/EX stage).

sw_stall 1..2 instructions class(jump_to_loop_end) () {
    class(jump_id);
} -> {
    special(end_of_loop);
}

sw_stall 1..3 instructions class(jump_to_loop_end) () {
    class(jump_ex);
} -> {
    special(end_of_loop);
}



//  two (nested) hardware loops may not end at the same address
sw_stall 0 instructions class(between_loop_ends) () {
    special(end_of_loop);
} -> {
    special(end_of_loop);
}

//   disallow software breakpoint on end-of-loop instruction
sw_stall 1 instructions () {
    special(pdc_sw_breakpoint);
} -> {
    special(end_of_loop);
}



// ensure loop count registers are witten before end of loop test

sw_stall 1..3 instructions class(zlp_cnt) () { ZC = ...; } -> { special(end_of_loop); }
sw_stall 1..3 instructions class(zlp_stt) () { ZE = ...; } -> { special(end_of_loop); }
sw_stall 1..3 instructions class(zlp_end) () { ZS = ...; } -> { special(end_of_loop); }

// Avoid setting *relative* LS, LE in delay_slots (of jump) because the PC has already changed:
sw_stall 1..2 cycles () { stage EX: ZE = ...; } -> { special(delay_slot_end); }
sw_stall 1..2 cycles () { stage EX: ZS = ...; } -> { special(delay_slot_end); }



component bypass_rule (ident name, reg R, int dist, trn src, stage src_stage, trn dst, pipe pipeP) {
  bypass dist cycles class(name) () {
    stage src_stage..WB: R$[#]`WB` = ...`WB` = # src`src_stage`;
  } -> {
    stage ID: #pipeP=dst = R$[#];
  }
}

// Integer Data Path
component bypass_X ( ident name, int dist, trn src, stage src_stage ) {
  instantiate bypass_rule (name, R, dist, src, src_stage, r_r1, pidR1);
  instantiate bypass_rule (name, R, dist, src, src_stage, r_r2, pidR2);
}

// bypasses for write port r_w1
instantiate bypass_X (bypass_from_EX, 1, texR1, EX); // <<<< EXAMPLE

// EXAMPLE: The above marked nested nML component instantiation is
// expanded in two steps (bypass_X, bypass_rule) as follows:

// bypass 1 cycles class(bypass_from_EX) () {
//   stage EX..WB: R$[#]`WB` = ...`WB` = # texR1`EX`;
// } -> {
//   stage ID: # pidR1 = r_r1 = R$[#];
// }

// bypass 1 cycles class(bypass_from_EX) () {
//   stage EX..WB: R$[#]`WB` = ...`WB` = # texR1`EX`;
// } -> {
//   stage ID: # pidR2 = r_r2 = R$[#];
// }

instantiate bypass_X (bypass_from_ME, 2, tmeR1, ME);

instantiate bypass_X (bypass_from_WB, 3, r_w1, WB);

// bypasses for write port r_w2
instantiate bypass_X (bypass_from_EX, 1, texR2, EX);

instantiate bypass_X (bypass_from_ME, 2, tmeR2, ME);

instantiate bypass_X (bypass_from_WB, 3, r_w2, WB);



component hw_stall_rule (ident name, reg R, int dist, trn src, stage src_stage) {
  hw_stall dist cycles class(name) () {
    stage src_stage..WB: R$[#]`WB` = ...`WB` = src`src_stage`;
  } -> {
    stage ID: ... = R$[#];
  }
}

// Integer Data Path
component hw_stall_X ( ident name, int dist, trn src, stage src_stage ) {
  instantiate hw_stall_rule (name, R, dist, src, src_stage);
}

// stall when result of multiplication is used in next instruction
instantiate hw_stall_X (hw_stall_mpy, 1, mpyC, ME); //
// EXAMPLE: The above marked nested nML component instantiation is
// expanded in (hw_stall_X, hw_stall_rule) as follows:

// hw_stall 1 cycles class(hw_stall_mpy) () {
//   stage ME..WB: R$[#]`WB` = ...`WB` = # mpyC`ME`;
// } -> {
//   stage ID:  ... = R$[#];
// }

instantiate hw_stall_X (hw_stall_mpy, 1, mpyD, ME); //

instantiate hw_stall_X (hw_stall_mpy, 1, lxC, ME); //
#if !defined(__programmers_view__)

// stall if next instruction is dependent on division results
hw_stall trn class(hw_stall_div_dep)  ()  { trn(div_busy_raw);  } -> { stage ID: ... = PL; } // quotient
hw_stall trn class(hw_stall_div_dep)  ()  { trn(div_busy_raw);  } -> { stage ID: ... = PH; } // remainder

// stall division while previous division is still active
hw_stall trn class(hw_stall_div_busy) ()  { trn(div_busy) ; } -> { stage EX: divide(divA, divB, wl, wh); }

// stall instructions that write to result register of division
hw_stall trn class(hw_stall_div_out)  ()  { trn(div_busy_wb); } -> { stage WB: PL = ...; }
hw_stall trn class(hw_stall_div_out)  ()  { trn(div_busy_wb); } -> { stage WB: PH = ...; }
#endif

